apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'

def app_version = "0.1"
def vcs_tag = "0.1"
def description = "Model and AIDL definition for MangaRx Plugins"
def web_url = "https://github.com/mario222k/manga-rx"
def git_url = "https://github.com/mario222k/manga-rx.git"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName app_version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            java.srcDirs += ['src/main/java','src/main/aidl']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$support_version"

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name description
                url web_url
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mario222k'
                        name 'Mario'
                        email 'mario222k@gmail.com'
                    }
                }
                scm {
                    connection git_url
                    developerConnection git_url
                    url web_url

                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'manga-rx-interface'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = git_url
        publish = true
        version {
            name = app_version
            desc = description
            released = new Date()
            vcsTag = vcs_tag
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'de.mario222k.mangarxinterface'
            artifactId 'manga-rx-interface'
            version app_version

            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/interface-release.aar")
        }
    }
}
